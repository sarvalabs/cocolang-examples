coco MT //Multi Tokens
      
class TokenMetadata:
    field tokenId U256
    field tokenURI String
    field supply U256

// For PISA 0.3.2 target use state persistent
state logic:
    // token ID -> metadata
    Tokens Map[U256]TokenMetadata
    // owner -> token ID -> balance
    BalanceOf Map[Identifier]Map[U256]U256
    // token ID -> operator -> approval
    OperatorApprovals Map[Identifier]Map[Identifier]Bool
    // total tokens minted
    TotalSupply U256
    ContractName String
    ContractSymbol String

event TransferSingle:
    topic operator Identifier
    topic from Identifier
    topic to Identifier
    topic id U256
    field value U256

event TransferBatch:
    topic operator Identifier
    topic from Identifier
    topic to Identifier

event ApprovalForAll:
    topic account Identifier
    topic operator Identifier
    topic approved Bool

// For PISA 0.3.2 target use endpoint deploy Init(name String, symbol String)
endpoint dynamic Init(name String, symbol String):
    mutate ContractName, ContractSymbol, Tokens, BalanceOf, OperatorApprovals, TotalSupply <- MT.Logic.ContractName, MT.Logic.ContractSymbol, MT.Logic.Tokens, MT.Logic.BalanceOf, MT.Logic.OperatorApprovals, MT.Logic.TotalSupply:
        ContractName = name
        ContractSymbol = symbol
        disperse Tokens <- make(Map[U256]TokenMetadata)
        disperse BalanceOf <- make(Map[Identifier]Map[U256]U256)
        disperse OperatorApprovals <- make(Map[Identifier]Map[Identifier]Bool)
        TotalSupply = 0

// For PISA 0.3.2 target use func readonly _exists(tokenId U256) -> (exists Bool)
function _exists(tokenId U256) -> (exists Bool):
    observe Tokens <- MT.Logic.Tokens:
        return exists Tokens[tokenId]?

// For PISA 0.3.2 target use func readonly _isApprovedForAll(account Address, operator Address) -> (isApproved Bool)
function _isApprovedForAll(account Identifier, operator Identifier) -> (isApproved Bool):
    observe OperatorApprovals <- MT.Logic.OperatorApprovals:
        if !OperatorApprovals[account]?:
            return isApproved false
        return isApproved OperatorApprovals[account][operator]

// For PISA 0.3.2 target use endpoint invoke readonly balanceOf(account Address, id U256) -> (balance U256)
endpoint balanceOf(account Identifier, id U256) -> (balance U256):
    observe BalanceOf <- MT.Logic.BalanceOf:
        if !BalanceOf[account]?:
            return balance U256(0)
        if !BalanceOf[account][id]?:
            return balance U256(0)
        return balance BalanceOf[account][id]

// For PISA 0.3.2 target use endpoint invoke readonly balanceOfBatch(accounts []Address, ids []U256) -> (balances []U256)
endpoint balanceOfBatch(accounts []Identifier, ids []U256) -> (balances []U256):
    if len(accounts) != len(ids):
        throw "Accounts and IDs must have the same length"
    observe BalanceOf <- MT.Logic.BalanceOf:
        balances = make([]U256, len(accounts))
        for i in range(0, len(accounts)):
            if !BalanceOf[accounts[i]]?:
                balances[i] = 0
                continue
            if !BalanceOf[accounts[i]][ids[i]]?:
                balances[i] = 0
                continue
            balances[i] = BalanceOf[accounts[i]][ids[i]]

// For PISA 0.3.2 target use endpoint invoke persistent setApprovalForAll(operator Address, approved Bool):
endpoint dynamic setApprovalForAll(operator Identifier, approved Bool):
    if operator == Identifier(Sender):
        throw "Setting approval status for self"
    mutate OperatorApprovals <- MT.Logic.OperatorApprovals:
        if !OperatorApprovals[Identifier(Sender)]?:
            disperse OperatorApprovals[Identifier(Sender)] <- make(Map[Identifier]Bool)
        OperatorApprovals[Identifier(Sender)][operator] = approved
    emit ApprovalForAll(Identifier(Sender), operator, approved)

// For PISA 0.3.2 target use endpoint invoke readonly isApprovedForAll(account Address, operator Address) -> (isApproved Bool)
endpoint isApprovedForAll(account Identifier, operator Identifier) -> (isApproved Bool):
    observe OperatorApprovals <- MT.Logic.OperatorApprovals:
        if !OperatorApprovals[account]?:
            return isApproved false
        return isApproved OperatorApprovals[account][operator]

// For PISA 0.3.2 target use func persistent _transfer(from Address, to Address, id U256, amount U256):
function dynamic _transfer(from Identifier, to Identifier, id U256, amount U256):
    observe BalanceOf <- MT.Logic.BalanceOf:
        if to == Identifier(0):
            throw "Transfer to the zero address"
        if !BalanceOf[from]?:
            throw "Insufficient balance for transfer"
        if !BalanceOf[from][id]?:
            throw "Insufficient balance for transfer"
        if BalanceOf[from][id] < amount:
            throw "Insufficient balance for transfer"
    mutate BalanceOf <- MT.Logic.BalanceOf:
        BalanceOf[from][id] -= amount
        if !BalanceOf[to]?:
            disperse BalanceOf[to] <- make(Map[U256]U256)
        if !BalanceOf[to][id]?:
            BalanceOf[to][id] = 0
        BalanceOf[to][id] += amount

// For PISA 0.3.2 target use endpoint invoke persistent safeTransferFrom(from Address, to Address, id U256, amount U256, data []Bytes):
endpoint dynamic safeTransferFrom(from Identifier, to Identifier, id U256, amount U256, data []Bytes):
    memory isApproved = _isApprovedForAll(account: from, operator: Identifier(Sender))
    if from != Identifier(Sender) && !isApproved:
        throw "Caller is not owner nor approved"

    _transfer(from, to, id, amount)
    emit TransferSingle(Identifier(Sender), from, to, id, amount)
    // TODO: Implement additional safety checks for recipient if required

// For PISA 0.3.2 target use endpoint invoke persistent safeBatchTransferFrom(from Address, to Address, ids []U256, amounts []U256, data []Bytes)
endpoint dynamic safeBatchTransferFrom(from Identifier, to Identifier, ids []U256, amounts []U256, data []Bytes):
    memory isApproved = _isApprovedForAll(account: from, operator: Identifier(Sender))
    if from != Identifier(Sender) && !isApproved:
        throw "Caller is not owner nor approved"
    if len(ids) != len(amounts):
        throw "IDs and amounts length mismatch"
    for i in range(0, len(ids)):
        _transfer(from, to, id: ids[i], amount: amounts[i])
    emit TransferBatch(Identifier(Sender), from, to)
    // TODO: Implement additional safety checks for recipient if required

// For PISA 0.3.2 target use endpoint invoke persistent mint(to Address, tokenId U256, amount U256, tokenURI String)
endpoint dynamic mint(to Identifier, tokenId U256, amount U256, tokenURI String):
    observe Tokens <- MT.Logic.Tokens:
        if !Tokens[tokenId]?:
            disperse Tokens[tokenId] <- TokenMetadata{tokenId: tokenId, tokenURI: tokenURI, supply: 0}
    if to == Identifier(0):
        throw "Mint to the empty address"
    mutate Tokens, BalanceOf, TotalSupply <- MT.Logic.Tokens, MT.Logic.BalanceOf, MT.Logic.TotalSupply:
        Tokens[tokenId].supply += amount
        if !BalanceOf[to]?:
            disperse BalanceOf[to] <- make(Map[U256]U256)
        if !BalanceOf[to][tokenId]?:
            BalanceOf[to][tokenId] = 0
        BalanceOf[to][tokenId] += amount
        TotalSupply += amount
    emit TransferSingle(Identifier(Sender), Identifier(0), to, tokenId, amount)

// For PISA 0.3.2 target use endpoint invoke readonly tokenURI(tokenId U256) -> (URI String)
endpoint tokenURI(tokenId U256) -> (URI String):
    observe Tokens <- MT.Logic.Tokens:
        if !Tokens[tokenId]?:
            throw "Token does not exist"
        return URI Tokens[tokenId].tokenURI

// For PISA 0.3.2 target use endpoint invoke readonly name() -> (Name String)
endpoint name() -> (Name String):
    observe ContractName <- MT.Logic.ContractName:
        return Name ContractName

// For PISA 0.3.2 target use endpoint invoke readonly symbol() -> (Symbol String)
endpoint symbol() -> (Symbol String):
    observe ContractSymbol <- MT.Logic.ContractSymbol:
        return Symbol ContractSymbol

// ERC-1155 optional enumeration extension (not fully implemented)
// For PISA 0.3.2 target use endpoint invoke readonly totalSupply() -> (Total U256)
endpoint totalSupply() -> (Total U256):
    observe TotalSupply <- MT.Logic.TotalSupply:
        return Total TotalSupply

// must implement ERC1155TokenReceiver(onERC1155Received(), onERC1155BatchReceived()) on wallet/broker/auction application
